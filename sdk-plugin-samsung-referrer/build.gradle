apply plugin: 'com.android.library'
apply plugin: 'maven-publish'
apply plugin: 'signing'

repositories {
    maven {
        url "https://oss.sonatype.org/content/repositories/staging/"
    }
}

android {
    compileSdkVersion rootProject.ext.coreCompileSdkVersion

    defaultConfig {
        minSdkVersion rootProject.ext.samsungReferrerMinSdkVersion
        targetSdkVersion rootProject.ext.coreTargetSdkVersion
    }
}

dependencies {
//    compileOnly files('libs/galaxyapps_sdk_referrer-release-v2.0.aar')

    // Add SDK via module.
    compileOnly project(':sdk-core')
    // Add SDK via Maven.
    // implementation 'com.alltrack.sdk:alltrack-android:0.0.1'

    // Add Samsung referrer lib via Maven.
}

// read local properties
File localPropsFile = project.rootProject.file('local.properties')
if (localPropsFile.exists()) {
    Properties p = new Properties()
    new FileInputStream(localPropsFile).withCloseable { is ->
        p.load(is)
    }
    p.each { name, value ->
        ext[name] = value
    }
}

task alltrackSamsungReferrerPluginAar (type: Copy) {
    dependsOn 'assembleRelease'

    from('build/outputs/aar/')
    include 'sdk-plugin-samsung-referrer-release.aar'
    destinationDir file('build/libs/')
    rename 'sdk-plugin-samsung-referrer-release.aar', "${project.name}.aar"

}

task alltrackSamsungReferrerPluginJar(type: Jar) {
    dependsOn 'packageReleaseAssets'
    dependsOn 'compileReleaseJavaWithJavac'

    from('build/intermediates/library_assets/release/packageReleaseAssets/out/') {
        into('assets')
    }
    from('build/intermediates/javac/release/classes/')

    archiveName "${project.name}.jar"
}

task androidJavadocs(type: Javadoc) {
    source = android.sourceSets.main.java.srcDirs
    classpath += project.files(android.getBootClasspath().join(File.pathSeparator))
    android.libraryVariants.all { variant ->
        if (variant.name == 'release') {
            classpath += variant.javaCompileProvider.get().classpath
        }
    }
}

task alltrackSamsungReferrerPluginJavadocsJar(type: Jar) {
    dependsOn 'androidJavadocs'
    classifier = 'javadoc'
    from androidJavadocs.destinationDir
}

task alltrackSamsungReferrerPluginSourcesJar(type: Jar) {
    classifier = 'sources'
    from android.sourceSets.main.java.srcDirs
}

artifacts {
    archives alltrackSamsungReferrerPluginJar
    archives alltrackSamsungReferrerPluginJavadocsJar
    archives alltrackSamsungReferrerPluginSourcesJar
}

publishing {
    publications {
        mavenAndroidSamsungReferrer(MavenPublication) {
            customizePom(pom)
            groupId rootProject.ext.alltrackGroupId
            artifactId 'alltrack-android-samsung-referrer'
            version rootProject.ext.coreVersionName

            artifact alltrackSamsungReferrerPluginJar
            artifact alltrackSamsungReferrerPluginJavadocsJar
            artifact alltrackSamsungReferrerPluginSourcesJar
        }
    }

    repositories {
        maven {
            url "https://oss.sonatype.org/service/local/staging/deploy/maven2"
            if (project.hasProperty("sonatypeUsername")) {
                credentials {
                    username sonatypeUsername
                    password sonatypePassword
                }
            }
        }
    }
}

def customizePom(pom) {
    pom.withXml {
        def root = asNode()

        // Add all items necessary for maven central publication.
        root.children().last() + {
            resolveStrategy = Closure.DELEGATE_FIRST
            description 'The Samsung Referrer plugin for Alltrack SDK for Android'
            name 'Alltrack Android SDK Samsung Referrer plugin'
            url 'https://github.com/khaledgoldman/alltrack-android-sdk'

            organization {
                name 'alltrack'
                url 'http://www.alltrack.com'
            }
            licenses {
                license {
                    name 'MIT License'
                    url 'http://www.opensource.org/licenses/mit-license.php'
                }
            }
            scm {
                url 'git@github.com:khaledgoldman/alltrack-android-sdk.git'
                connection 'scm:git:git@github.com:khaledgoldman/alltrack-android-sdk.git'
                developerConnection 'scm:git:git@github.com:khaledgoldman/alltrack-android-sdk.git'
            }
            developers {
                developer {
                    name 'Khaled Goldman'
                    email 'khaledgoldman@yahoo.com'
                }
            }
        }
    }
}

model {
    tasks.generatePomFileForMavenAndroidSamsungReferrerPublication {
        destination = file("${project.buildDir}/generated-pom.xml")
    }
    /* TODO check if/how to replace this tasks
    tasks.publishMavenAndroidSamsungReferrerPublicationToMavenLocal {
        dependsOn project.tasks.signArchives
    }
    tasks.publishMavenAndroidSamsungReferrerPublicationToMavenRepository {
        dependsOn project.tasks.signArchives
    }
    */
}

signing {
    sign publishing.publications.mavenAndroidSamsungReferrer
}
