apply plugin: 'com.android.library'
apply plugin: 'maven-publish'
apply plugin: 'signing'

android {
    compileSdkVersion rootProject.ext.coreCompileSdkVersion

    defaultConfig {
        minSdkVersion rootProject.ext.coreMinSdkVersion
        targetSdkVersion rootProject.ext.coreTargetSdkVersion
        consumerProguardFiles 'alltrack-proguard-rules.txt'
    }

    buildTypes {
        release {
            minifyEnabled true
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
        }
    }
}

// read local properties
File localPropsFile = project.rootProject.file('local.properties')
if (localPropsFile.exists()) {
    Properties p = new Properties()
    new FileInputStream(localPropsFile).withCloseable { is ->
        p.load(is)
    }
    p.each { name, value ->
        ext[name] = value
    }
}

dependencies {
    implementation fileTree(dir: 'libs', include: ['*.jar'])
}

task alltrackCoreAarRelease(type: Copy) {
    dependsOn 'assembleRelease'

    from('build/outputs/aar/')
    include 'sdk-core-release.aar'
    destinationDir file('build/libs/')
    rename 'sdk-core-release.aar', 'alltrack-sdk-release.aar'
}

task alltrackCoreAarDebug(type: Copy) {
    dependsOn 'assembleDebug'

    from('build/outputs/aar/')
    include 'sdk-core-debug.aar'
    destinationDir file('build/libs/')
    rename 'sdk-core-debug.aar', 'alltrack-sdk-debug.aar'
}

task alltrackCoreJarRelease(type: Jar) {
    dependsOn 'compileReleaseJavaWithJavac'
    from('build/intermediates/javac/release/classes/')
    archiveName "alltrack-sdk-release.jar"
}

task alltrackCoreJarDebug(type: Jar) {
    dependsOn 'compileDebugJavaWithJavac'
    from('build/intermediates/javac/debug/classes/')
    archiveName "alltrack-sdk-debug.jar"
}

task androidJavadocs(type: Javadoc) {
    source = android.sourceSets.main.java.srcDirs
    classpath += project.files(android.getBootClasspath().join(File.pathSeparator))
    // JDK 1.8 is more strict then 1.7. Have JDK 1.8 behave like 1.7 for javadoc generation.
    if (org.gradle.internal.jvm.Jvm.current().getJavaVersion() == JavaVersion.VERSION_1_8) {
        options.addStringOption('Xdoclint:none', '-quiet')
    }
}

task alltrackCoreAndroidJavadocsJar(type: Jar) {
    dependsOn 'androidJavadocs'
    classifier = 'javadoc'
    from androidJavadocs.destinationDir
}

task alltrackCoreAndroidSourcesJar(type: Jar) {
    classifier = 'sources'
    from android.sourceSets.main.java.srcDirs
}

artifacts {
    archives alltrackCoreJarRelease
    archives alltrackCoreAndroidJavadocsJar
    archives alltrackCoreAndroidSourcesJar
}

publishing {
    publications {
//        mavenAndroidCoreJar(MavenPublication) {
//            customizePomForJar(pom)
//            groupId rootProject.ext.alltrackGroupId
//            artifactId 'alltrack-android'
//            version rootProject.ext.coreVersionName
//
//            artifact alltrackCoreJarRelease
//            artifact alltrackCoreAndroidJavadocsJar
//            artifact alltrackCoreAndroidSourcesJar
//        }

        mavenAndroidCoreAar(MavenPublication) {
            customizePomForAar(pom)
            groupId rootProject.ext.alltrackGroupId
            artifactId 'alltrack-android'
            version rootProject.ext.coreVersionName

            artifact("$buildDir/outputs/aar/sdk-core-release.aar"){
                builtBy tasks.getByPath("alltrackCoreAarRelease")
            }
            artifact alltrackCoreAndroidJavadocsJar
            artifact alltrackCoreAndroidSourcesJar
        }

    }

    repositories {
        maven {
            if (rootProject.ext.coreVersionName.toString().contains('SNAPSHOT')) {
                println "Using Maven SNAPSHOT repository for publishing"
                url "https://oss.sonatype.org/content/repositories/snapshots/"
            } else {
                url "https://oss.sonatype.org/service/local/staging/deploy/maven2"
            }
            if (project.hasProperty("sonatypeUsername")) {
                credentials {
                    username sonatypeUsername
                    password sonatypePassword
                }
            }
        }
    }
}

def customizePomForJar(pom) {
    pom.withXml {
        // Add all items necessary for maven central publication.
        asNode().children().last() + {
            resolveStrategy = Closure.DELEGATE_FIRST
            description 'The Alltrack SDK for Android'
            name 'Alltrack Android SDK'
            url 'https://github.com/khaledgoldman/alltrack-android-sdk'
            organization {
                name 'alltrack'
                url 'http://www.alltrack.com'
            }
            licenses {
                license {
                    name 'MIT License'
                    url 'http://www.opensource.org/licenses/mit-license.php'
                }
            }
            scm {
                url 'git@github.com:khaledgoldman/alltrack-android-sdk.git'
                connection 'scm:git:git@github.com:khaledgoldman/alltrack-android-sdk.git'
                developerConnection 'scm:git:git@github.com:khaledgoldman/alltrack-android-sdk.git'
            }
            developers {
                developer {
                    name 'Khaled Goldman'
                    email 'khaledgoldman@yahoo.com'
                }
            }
        }
    }
}

def customizePomForAar(pom) {
    pom.withXml {
        // Remove the existing packaging field, as it has to be overridden with type aar
        asNode().remove(asNode().get("packaging"))

        // Add all items necessary for maven central publication.
        asNode().children().last() + {
            resolveStrategy = Closure.DELEGATE_FIRST
            description 'The Alltrack SDK for Android'
            name 'Alltrack Android SDK'
            url 'https://github.com/khaledgoldman/alltrack-android-sdk'
            packaging 'aar'
            organization {
                name 'alltrack'
                url 'http://www.alltrack.com'
            }
            licenses {
                license {
                    name 'MIT License'
                    url 'http://www.opensource.org/licenses/mit-license.php'
                }
            }
            scm {
                url 'git@github.com:khaledgoldman/alltrack-android-sdk.git'
                connection 'scm:git:git@github.com:khaledgoldman/alltrack-android-sdk.git'
                developerConnection 'scm:git:git@github.com:khaledgoldman/alltrack-android-sdk.git'
            }
            developers {
                developer {
                    name 'Pedro Silva'
                    email 'pedro@alltrack.com'
                }
                developer {
                    name 'Ugljesa Erceg'
                    email 'ugljesa@alltrack.com'
                }
            }
        }
    }
}

model {
//    tasks.generatePomFileForMavenAndroidCoreJarPublication {
//        destination = file("${project.buildDir}/generated-pom.xml")
//    }
    /* TODO check if/how to replace this tasks
    tasks.publishMavenAndroidCorePublicationToMavenLocal {
        dependsOn project.tasks.signArchives
    }
    tasks.publishMavenAndroidCorePublicationToMavenRepository {
        dependsOn project.tasks.signArchives
    }
    */
    tasks.generatePomFileForMavenAndroidCoreAarPublication {
        destination = file("${project.buildDir}/generated-pom.xml")
    }
    /* TODO check if/how to replace this tasks
    tasks.publishMavenAndroidCoreAARPublicationToMavenLocal {
        dependsOn project.tasks.signArchives
    }
    tasks.publishMavenAndroidCoreAARPublicationToMavenRepository {
        dependsOn project.tasks.signArchives
    }
    */
}

signing {
//    sign publishing.publications.mavenAndroidCoreJar
    sign publishing.publications.mavenAndroidCoreAar
}
